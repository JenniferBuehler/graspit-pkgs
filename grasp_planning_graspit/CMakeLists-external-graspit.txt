# Builds external graspit fork (https://github.com/JenniferBuehler/graspit).
# The parent script needs to define the "GLOBAL_OUTPUT_PATH" variable,
# which will be used as output directory for all *.lib, *.dll, *.a, *.so, *.pdb files.
# If a certain branch is to be chosen, put it in the variable "GRASPIT_BRANCH_NAME"

include(ExternalProject)

ExternalProject_Add(
    graspit_ext

    GIT_REPOSITORY "https://github.com/JenniferBuehler/graspit"
    GIT_TAG "master"
    
    UPDATE_COMMAND ""
    PATCH_COMMAND ""
 
    TMP_DIR graspit/tmp
    STAMP_DIR graspit/src/graspit-stamp
    DOWNLOAD_DIR graspit/src
    SOURCE_DIR graspit/src/graspit
    BINARY_DIR graspit/src/graspit-build
    INSTALL_DIR graspit/install

    GIT_TAG ${GRASPIT_BRANCH_NAME}

    # unset CATKIN_DEVEL_PREFIX to enforce
    # undefining the variable and prevent building with catkin 
    CMAKE_ARGS -DCMAKE_INSTALL_PREFIX=${GLOBAL_OUTPUT_PATH} -UCATKIN_DEVEL_PREFIX -DBUILD_STATIC_LIB:BOOL=true -DBUILD_TESTS:BOOL=false -DSKIP_SHARED_LIB=true
   
    BUILD_COMMAND make
    INSTALL_COMMAND make install 
    TEST_COMMAND ""
)

ExternalProject_Add_Step(
  graspit_ext RenameBin
  COMMAND ${CMAKE_COMMAND} -E rename ${GLOBAL_OUTPUT_PATH}/bin/graspit_simulator ${GLOBAL_OUTPUT_PATH}/bin/graspit_simulator_standalone
  DEPENDEES install
)

set(GRASPIT_INCLUDE_DIRS 
    ${CMAKE_CURRENT_BINARY_DIR}/graspit/src/graspit
    ${CMAKE_CURRENT_BINARY_DIR}/graspit/src/graspit/include
    ${CMAKE_CURRENT_BINARY_DIR}/graspit/src/graspit/src/
)


# for building with catkin, link_directories should not be used: instead, add the full path to the
# library to use in target_link_libraries().
#set(GRASPIT_LIBRARY "${CMAKE_CURRENT_BINARY_DIR}/graspit/src/graspit-build/${CMAKE_SHARED_LIBRARY_PREFIX}graspit${CMAKE_SHARED_LIBRARY_SUFFIX}")
set(GRASPIT_LIBRARY "${GLOBAL_OUTPUT_PATH}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}graspit_static${CMAKE_STATIC_LIBRARY_SUFFIX}")

set(GRASPIT_EXECUTABLE "${GLOBAL_OUTPUT_PATH}/bin/graspit_simulator_standalone")

# import the configuration file (has problems with catkin, and is not really required)
# include(${GLOBAL_OUTPUT_PATH}/lib/graspit-static-lib-targets.cmake)
